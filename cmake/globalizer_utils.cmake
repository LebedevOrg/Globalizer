function (get_gcc_version arg_verstr)
  string(REGEX MATCHALL "[0-9]+" GCC_VERSION_COMPONENTS ${CMAKE_CXX_COMPILER_VERSION})
  list(GET GCC_VERSION_COMPONENTS 0 GCC_MAJOR)
  list(LENGTH GCC_VERSION_COMPONENTS VERSION_STR_LEN)
  if(VERSION_STR_LEN GREATER 1)
    list(GET GCC_VERSION_COMPONENTS 1 GCC_MINOR)
  else()
    set(GCC_MINOR 0)
  endif()
  set(${arg_verstr} ${GCC_MAJOR}.${GCC_MINOR} PARENT_SCOPE)
endfunction (get_gcc_version)

macro(subdirlist result curdir)
  file(GLOB children RELATIVE ${curdir} ${curdir}/*)
  set(dirlist "")
  foreach(child ${children})
    if(IS_DIRECTORY ${curdir}/${child} AND NOT (${child} MATCHES "[.]"))
        list(APPEND dirlist ${curdir}/${child})
    endif()
  endforeach()
  set(${result} ${dirlist})
endmacro()

macro(replace_compiler_flag old_value new_value)
  foreach(flag_var
            CMAKE_C_FLAGS CMAKE_C_FLAGS_DEBUG CMAKE_C_FLAGS_RELEASE
            CMAKE_C_FLAGS_MINSIZEREL CMAKE_C_FLAGS_RELWITHDEBINFO
            CMAKE_CXX_FLAGS CMAKE_CXX_FLAGS_DEBUG CMAKE_CXX_FLAGS_RELEASE
            CMAKE_CXX_FLAGS_MINSIZEREL CMAKE_CXX_FLAGS_RELWITHDEBINFO)
      if(${flag_var} MATCHES ${old_value})
        string(REGEX REPLACE ${old_value} ${new_value} ${flag_var} "${${flag_var}}")
      endif()
    endforeach(flag_var)
endmacro()

function (GLOBALIZER_initialize)
  message("USE COMPILER:" ${CMAKE_CXX_COMPILER_ID})
  if (CMAKE_CXX_COMPILER_ID MATCHES "GNU")
    set(IS_GNU_COMPILER True PARENT_SCOPE)
    get_gcc_version(GCC_VER)
    set(GLOBALIZER_GCC_VERSION gcc-${GCC_VER} PARENT_SCOPE)
	message("USE GNU COMPILER")
  elseif (CMAKE_CXX_COMPILER_ID MATCHES "Intel")
    set(IS_INTEL_COMPILER True PARENT_SCOPE)
	message("USE Intel COMPILER")
  elseif (CMAKE_CXX_COMPILER_ID MATCHES "Clang[+][+]")
    set(IS_CLANG_COMPILER True PARENT_SCOPE)
	message("USE Clang COMPILER")
  elseif (CMAKE_CXX_COMPILER_ID MATCHES "MSVC")
    set(IS_MICROSOFT_COMPILER True PARENT_SCOPE)
    if (${MSVC90})
      set(GLOBALIZER_VC_VERSION "vc9" PARENT_SCOPE)
    elseif (${MSVC10})
      set(GLOBALIZER_VC_VERSION "vc10" PARENT_SCOPE)
    elseif (${MSVC11})
      message(FATAL_ERROR "Microsoft Visual Studio 2012 is not supported")
    elseif (${MSVC12})
      set(GLOBALIZER_VC_VERSION "vc12" PARENT_SCOPE)
    elseif (${MSVC14})
      set(GLOBALIZER_VC_VERSION "vc14" PARENT_SCOPE)
    endif()
	message("USE MSVC COMPILER")
  else()
    set(IS_GNU_COMPILER True PARENT_SCOPE)
    get_gcc_version(GCC_VER)
    set(GLOBALIZER_GCC_VERSION gcc-${GCC_VER} PARENT_SCOPE)
    message("Unknown compiler")
  endif()

  if (${CMAKE_SIZEOF_VOID_P} EQUAL 8)
    set(GLOBALIZER_TARGET_ARCH "x64" PARENT_SCOPE)
  elseif (${CMAKE_SIZEOF_VOID_P} EQUAL 4)
    set(GLOBALIZER_TARGET_ARCH "x86" PARENT_SCOPE)
  else()
    message(FATAL_ERROR "Unsupported architecture")
  endif()
endfunction (GLOBALIZER_initialize)

function (set_path arg_var arg_value)
  file(TO_CMAKE_PATH ${arg_value} VAR_CMAKE_PATH)
  set(${arg_var} ${VAR_CMAKE_PATH} PARENT_SCOPE)
endfunction (set_path)

function (select_platform_depended_library arg_lib arg_envlibx86 arg_envlibx64)
  if (GLOBALIZER_TARGET_ARCH MATCHES "x86")
    set_path(LIB ${arg_envlibx86})
  elseif (GLOBALIZER_TARGET_ARCH MATCHES "x64")
    set_path(LIB ${arg_envlibx64})
  endif()
  set(${arg_lib} ${LIB} PARENT_SCOPE)
endfunction (select_platform_depended_library)

function (get_platform_depended_library_path arg_out)
  if (WIN32)
    if (IS_MICROSOFT_COMPILER)
      set(${arg_out} lib/win/${GLOBALIZER_VC_VERSION}/${GLOBALIZER_TARGET_ARCH} PARENT_SCOPE)
    elseif (IS_INTEL_COMPILER)
      message(FATAL_ERROR "Intel compiler support not implemented for Windows")
    endif()
  elseif (UNIX)
#    if (IS_GNU_COMPILER)
      set(${arg_out} lib/lnx PARENT_SCOPE)
#    elseif (IS_INTEL_COMPILER)
#      message(FATAL_ERROR "Intel compiler support not implemented for UNIX")
#    endif()
  else()
    message(FATAL_ERROR "Unsupported operating system")
  endif()
endfunction (get_platform_depended_library_path)

function (get_libname_from_path arg_path arg_name arg_dir)
  get_filename_component(LIBFILENAME ${arg_path} NAME_WE)
  get_filename_component(LIBDIRPATH ${arg_path} DIRECTORY)
  set_path(LIBDIR ${LIBDIRPATH})
  string(REGEX REPLACE "^lib" "" LIBNAME ${LIBFILENAME})
  set(${arg_name} ${LIBNAME} PARENT_SCOPE)
  set(${arg_dir} ${LIBDIR} PARENT_SCOPE)
endfunction (get_libname_from_path)

macro(print_build_config)
  message("CXX compiler: " ${CMAKE_CXX_COMPILER})
  message("Common compiler flags: " ${CMAKE_CXX_FLAGS})
  message("Build type: " ${CMAKE_BUILD_TYPE})
  message("Target arch: " ${GLOBALIZER_TARGET_ARCH})

  get_directory_property( DirDefs DIRECTORY ${CMAKE_SOURCE_DIR} COMPILE_DEFINITIONS)
  message( "Compile time definitions: " ${DirDefs})

endmacro()
